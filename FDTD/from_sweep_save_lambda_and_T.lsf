# 该脚本将 横坐标-波长 写入 "lambda.txt"
#      将 扫描得到的纵坐标透射率写入 "T.txt"
# 分隔符都是制表符 '\t', 你可以直接将数据复制到Excel里面,
# 也可以使用 MATLAB 或者 python 自己进行处理

# 测折射率的监视器的名字
monitor_name = "T";
# 最顶层扫描的名字
sweep_name = "sweep";

# ------------lambda------------
# --- 方法一, 需要运行当前文件的模拟
# 获得测折射率监听器的数据
# T_dataset = getresult(monitor_name, "T");
# 波长, 单位是微米
# lambda = T_dataset.lambda * 1e6;

# --- 方法二 从监听器获取设定的属性, 计算出波长对应的横坐标
# monitor_property 获得监视器属性
min_wl = getnamed(monitor_name, "minimum wavelength");
max_wl = getnamed(monitor_name, "maximum wavelength");
points = getnamed(monitor_name, "frequency points");
lambda = linspace(min_wl, max_wl, points)*1e6; # 单位微米


# 如果lambda文件不存在则写入lambda
out = fileexists("lambda.txt");
if(!out) {
    write("lambda.txt", "lambda");
    write("lambda.txt", num2str(lambda));  # 只能写字符串
}

# ------------T-----------------
# 扫描的结果
T = getsweepdata(sweep_name, "T");

# T矩阵的形状
T_size = size(T);
# 行数
# r_size = T_size(1);
# 列数
c_size = T_size(2);

# 将第一行列名写入文件, 分隔符是制表符'\t'
columns = "";
for(i=1:c_size-1){
    columns = columns + "T" + num2str(i) + "	";
}
columns = columns + "T" + num2str(c_size);
write("T.txt", columns);
# 将透射率的绝对值写入文件
write("T.txt", num2str(-T));
